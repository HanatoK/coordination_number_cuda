cmake_minimum_required(VERSION 3.16 FATAL_ERROR)
include(CheckIncludeFile)
project(coordination_number LANGUAGES CXX)

option(USE_CUDA "Use the CUDA library (if your want to run NAMD on NVIDIA GPUs)" ON)
option(USE_HIP "Use the HIP library (if your want to run NAMD on AMD GPUs)" OFF)

if(NOT(USE_CUDA OR USE_HIP))
  message(FATAL_ERROR "This plugin depends on either CUDA or HIP")
endif()

if(NOT DEFINED CMAKE_CUDA_STANDARD)
  set(CMAKE_CUDA_STANDARD 17)
  set(CMAKE_CUDA_STANDARD_REQUIRED ON)
endif()

find_package(fmt REQUIRED)
find_package(CLI11 REQUIRED)

if(USE_CUDA)
  find_package(CUDAToolkit REQUIRED)
  include(CheckLanguage)
  check_language(CUDA)
  enable_language(CUDA)
  if(NOT DEFINED CMAKE_CUDA_ARCHITECTURES)
    set(CMAKE_CUDA_ARCHITECTURES native)
  endif()
  add_definitions(-DUSE_CUDA)
  set(coordination_number_cuda_bin "${PROJECT_NAME}_cuda")
  add_executable(${coordination_number_cuda_bin} common.h common.cpp gpu_kernel.h gpu_kernel.cu test.cpp)
  set_property(TARGET ${coordination_number_cuda_bin} PROPERTY LANGUAGE CUDA)
  target_link_libraries(${coordination_number_cuda_bin} PRIVATE CUDA::cudart CUDA::nvtx3 fmt)
endif()

if(USE_HIP)
  find_package(hip REQUIRED)
  find_package(hipcub REQUIRED)
  include(CheckLanguage)
  check_language(HIP)
  enable_language(HIP)
  message(STATUS "CXX_FLAGS = ${CXX_FLAGS}")
  if(NOT DEFINED CMAKE_HIP_ARCHITECTURES)
    set(CMAKE_HIP_ARCHITECTURES native)
  endif()
  add_definitions(-DUSE_HIP)
  set(coordination_number_hip_bin "${PROJECT_NAME}_hip")
  set_source_files_properties(common.cpp PROPERTIES LANGUAGE CXX)
  set_source_files_properties(test.cpp PROPERTIES LANGUAGE CXX)
  set_source_files_properties(gpu_kernel.cu PROPERTIES LANGUAGE HIP)
  add_executable(${coordination_number_hip_bin} common.h common.cpp gpu_kernel.h hip_defines.h gpu_kernel.cu test.cpp)
  target_link_libraries(${coordination_number_hip_bin} PRIVATE hip::device hip::host fmt)
endif()

